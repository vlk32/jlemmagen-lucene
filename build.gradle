plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "net.nemerosa.versioning" version "2.12.0" apply true
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation "org.apache.lucene:lucene-analyzers-common:${lucene_version}"
    implementation 'eu.hlavki.text:jlemmagen:1.0.1'
    testImplementation "org.apache.lucene:lucene-test-framework:${lucene_version}"
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
}

versioning {
    branchEnv = [ 'CI_COMMIT_BRANCH', 'BRANCH_NAME' ]
}

group = 'eu.hlavki.text'
version = versioning.info.tag && !versioning.info.dirty ? versioning.info.tag : versioning.info.display
sourceCompatibility = '1.8'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('sonatypeUploadUser')
                password = findProperty('sonatypeUploadPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                name.set("jlemmagen-lucene")
                description.set("Integration of jlemmagen with lucene")
                url.set("https://github.com/hlavki/jlemmagen-lucene")
                licenses {
                    license {
                        name.set("Apache License 2.0")
                        url.set("https://www.apache.org/licenses/LICENSE-2.0")
                    }
                }
                developers {
                    developer {
                        id.set("hlavki")
                        name.set("Michal Hlavac")
                        email.set("miso@hlavki.eu")
                    }
                }
                scm {
                    connection.set("scm:git:https://github.com/hlavki/jlemmagen-lucene")
                    developerConnection.set("scm:git:https://github.com/hlavki/jlemmagen-lucene")
                    url.set("https://github.com/hlavki/jlemmagen-lucene")
                }

            }
        }
    }
}

signing {
    useGpgCmd()
    sign configurations.archives
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        versioning.info.tag && !versioning.info.dirty
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

